#% text_encoding = iso8859_1
_package user

_pragma(classify_level=advanced, topic={api})
##
## Base exemplar for generated APIs from `openapi_interface_generator` script.
##
def_slotted_exemplar(
	:openapi_interface_base,
	{
	},
	{@user:openapi_validator_mixin})
$

_pragma(classify_level=advanced, topic={api})
## @return {sw:char16_vector}
openapi_interface_base.define_shared_constant(
	:url_encoded_chars,
	" !#$%&'()*+,/:;=?@[]",
	:private)
$

_pragma(classify_level=advanced, topic={api})
_abstract _private _method openapi_interface_base.api_base_path
	## Get API base path. To be overridden by a shared_constant.
	## @return {sw:char16_vector}
_endmethod
$

_pragma(classify_level=advanced, topic={api})
_private _method openapi_interface_base.build_uri(base_uri, path_params, query_params)
	## Build URI from parts.
	## @param {sw:char16_vector} base_uri
	## @param {sw:equality_hash_table} path_params
	## @param {sw:equality_hash_table} query_params
	## @return {sw:char16_vector}
	_local uri << base_uri

	_for key, value _over path_params.fast_keys_and_elements()
	_loop
		_local pattern << write_string("\{", key, "}")
		_local encoded_value << _self.url_encode(value.write_string)
		uri << sw_regexp.new(pattern).replace_all(uri, encoded_value)
	_endloop

	_local query_items << rope.new()
	_for key, value _over query_params.fast_keys_and_elements()
	_loop
		_local encoded_value << _self.url_encode(value.write_string)
		_local query_item << write_string(key, "=", encoded_value)
		query_items.add(query_item)
	_endloop
	_if _not query_items.empty?
	_then
		uri << uri + "?" + query_items.join_as_strings("&")
	_endif

	_return _self.api_base_path + uri
_endmethod
$

_pragma(classify_level=advanced, topic={api})
_private _method openapi_interface_base.get_headers(method, uri)
	## Get headers for method/uri.
	## @param {sw:char16_vector} method
	## @param {sw:char16_vector} uri
	## @return {sw:equality_property_list}
	_local headers << sw:equality_property_list.new()
	headers["Accept"] << "application/json"
	_return headers
_endmethod
$

_pragma(classify_level=advanced, topic={api})
_private _method openapi_interface_base.url_encode(value)
	## URL Encode a value.
	## @param {sw:char16_vector} value
	## @return {sw:char16_vector}
	_local encoded_value << ""
	_for chr _over value.fast_elements()
	_loop
		_if _self.url_encoded_chars.includes?(chr)
		_then
			encoded_value +<< write_string(%%, chr.value.as_hex())
		_else
			encoded_value +<< chr
		_endif
	_endloop
	_return encoded_value
_endmethod
$

_pragma(classify_level=advanced, topic={api})
_private _method openapi_interface_base.url_decode(value)
	## URL Decode a value.
	## @param {sw:char16_vector} value
	## @return {sw:char16_vector}
	_local decoded_value << ""
	_local index << 1
	_while index < value.size
	_loop
		_local chr << value[index]
		_if chr _is %%
		_then
			# This might crash. It is up to the caller to validate the input.
			_local chr_hex << value[index + 1] + value[index + 2]
			_local chr_int << chr_hex.as_radix_integer(16)
			decoded_value +<< character.from_value(chr_int)
			index +<< 3
		_else
			decoded_value +<< chr
			index +<< 1
		_endif
	_endloop
	_return decoded_value
_endmethod
$

_pragma(classify_level=advanced, topic={api})
_private _method openapi_interface_base.do_request(method, uri, headers, _optional body)
	## Fire a request.
	## @param {sw:char16_vector} method HTTP Method.
	## @param {sw:char16_vector} uri URI to call.
	## @param {sw:equality_property_list} headers Headers to send.
	## @param {sw:char16_vector|sw:equality_property_list|sw:unset} body Body to send.
	## @return {sw:char16_vector} Response body.
	## @return {sw:http} http thing.
	_local magik_http << http.new(uri)
	_local request_headers << rope.new()
	_for key, value _over headers.fast_keys_and_elements()
	_loop
		_local pair << {key, value}
		request_headers.add_last(pair)
	_endloop
	_local payload <<
		_if body _is _unset
		_then
			>> _unset
		_elif body.is_kind_of?(sw:char16_vector)
		_then
			# Raw data.
			>> body
		_elif body.responds_to?(:|to_api()|)
		_then
			_local encoder << sw:json_encoder.new()
			>> encoder.generate_string(body.to_api())
		_else
			_local encoder << sw:json_encoder.new()
			>> encoder.generate_string(body)
		_endif
	_local response_body << magik_http.perform(method.lowercase + :|()|, request_headers)
	_local response_status_code << magik_http.status_code
	_if response_status_code _div 100 <> 2
	_then
		condition.raise(
			:api_response_error,
			:status_code, response_status_code,
			:body, response_body)
	_endif
	_return response_body, magik_http
_endmethod
$
